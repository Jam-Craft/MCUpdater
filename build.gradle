apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'application'
apply plugin: 'launch4j'

mainClassName = 'mcupdater.ServerMain'

group = 'mcupdater'

repositories {
	mavenCentral()
	maven {
		name = 'minecraft'
		url = 'https://libraries.minecraft.net/'
	}

}

buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath group: 'edu.sc.seis.gradle', name: 'launch4j', version: '1.0.6'
	}
}

jar {
	classifier = getCommitHash()
	manifest{
		attributes 'Main-Class': mainClassName
	}
}

dependencies {
	compile group: 'com.google.code.gson', name: 'gson', version:'1.7.2'
	compile group: 'log4j', name:'log4j', version:'1.2.7'
	compile group: 'net.minecraft', name: 'launchwrapper', version:'1.9'
	compile group: 'commons-io', name: 'commons-io', version: '2.4' 
	compile group: 'com.google.guava', name: 'guava', version: '17.0'

}

def getCommitHash() {
    def gitFolder = "$projectDir/.git/"
    def takeFromHash = 12
    def head = new File(gitFolder + "HEAD").text.split(":")
    def isCommit = head.length == 1
 
    if(isCommit) return head[0].trim().take(takeFromHash)
 
    def refHead = new File(gitFolder + head[1].trim())
    refHead.text.trim().take takeFromHash
}